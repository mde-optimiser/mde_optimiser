module uk.ac.kcl.inf.mdeoptimiser.languages.GenerateMopt

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

//Configure target location for generating the ide project

//var ideProjectName = "uk.ac.kcl.inf.mdeoptimiser.interfaces.eclipse.mopt.xtext.ide"
//var ideProjectPath = "${rootPath}/../../../interfaces/eclipse/src/${ideProjectName}"
//var ideSrc = "${ideProjectPath}/src/"
//var ideSrcGen = "${ideProjectPath}/src-gen/"
//var ideMetaInf = "${ideProjectPath}/META-INF"

/**
 * The generic IDE and Eclipse IDE projects are combined in the same project
 */

var eclipseProjectName = "uk.ac.kcl.inf.mdeoptimiser.interfaces.eclipse.ui"
var eclipseProjectPath = "${rootPath}/../../../interfaces/eclipse/src/${eclipseProjectName}"
var eclipseSrc = "${eclipseProjectPath}/src/"
var eclipseSrcGen = "${eclipseProjectPath}/src-gen/"
var eclipseMetaInf = "${eclipseProjectPath}/META-INF"

var eclipseUiTestsProjectName = "uk.ac.kcl.inf.mdeoptimiser.interfaces.eclipse.ui.tests"
var eclipseUiTestsProjectPath = "${rootPath}/../../../interfaces/eclipse/src/${eclipseUiTestsProjectName}"
var eclipseUiTestsSrc = "${eclipseUiTestsProjectPath}/src/"
var eclipseUiTestsSrcGen = "${eclipseUiTestsProjectPath}/src-gen/"
var eclipseUiTestsMetaInf = "${eclipseUiTestsProjectPath}/META-INF"


Workflow {

	component = XtextGenerator {
		
		configuration = {
			
			
			
			
			code = {
				preferXtendStubs = false
			}
		
			project = StandardProjectConfig {
				baseName = "uk.ac.kcl.inf.mdeoptimiser.languages.mopt.xtext"
				rootPath = rootPath

				runtimeTest = {
					enabled = true
				}
				
				
			
				eclipsePlugin = {
					enabled = true
					name = eclipseProjectName
					root = eclipseProjectPath
					src = eclipseSrc
					srcGen = eclipseSrcGen
					metaInf = eclipseMetaInf
				}
				
				eclipsePluginTest = {
					enabled = true
					name = eclipseUiTestsProjectName
					root = eclipseUiTestsProjectPath
					src = eclipseUiTestsSrc
					srcGen = eclipseUiTestsSrcGen
					metaInf = eclipseUiTestsMetaInf
				}

				// Merge the Generic IDE and UI projects
				// https://www.eclipse.org/Xtext/documentation/302_configuration.html
				genericIde = {
					enabled = true
					name = eclipseProjectName
					root = eclipseProjectPath
					src = eclipseSrc
					srcGen = eclipseSrcGen
					metaInf = eclipseMetaInf
				}

				createEclipseMetaData = true
				mavenLayout = true
				
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = true
			}
		}
		language = StandardLanguage {
			name = "uk.ac.kcl.inf.mdeoptimiser.languages.Mopt"
			fileExtensions = "mopt"

			generator = {
				generateXtendStub = false
			}

			scopeProvider = {
				generateXtendStub = false		
			}
			
			xbaseSupport = {
				generateXtendInferrer = false
			}

			serializer = {
				generateStub = false
				
			}

			labelProvider = {
				generateXtendStub = true
			}

			quickFixProvider = {
				generateXtendStub = true
			}
			
			outline = {
				generateXtendStub = true
			}
			
			contentAssist = {
				generateXtendStub = true
			}
			
			
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
				generateXtendStub = false
			}
			
			junitSupport = {
				junitVersion = "5"
				generateXtendStub = false
				generateStub = false
				 
			}
		}		
	}
}
